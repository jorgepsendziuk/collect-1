import com.android.ddmlib.DdmPreferences

/**************************************************************************************************
 GRADLE CONFIG
 *************************************************************************************************/

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

DdmPreferences.setTimeOut(60000)

ant.condition(property: "os", value: "windows") { os(family: "windows") }
ant.condition(property: "os", value: "unix") { os(family: "unix") }

// Build numbers were manually set until 1067
def LEGACY_BUILD_NUMBER_OFFSET = 1067

// Based on http://stackoverflow.com/questions/17097263#24121734
def getMasterCommitCount = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            switch(ant.properties.os){
                case 'windows':
                    commandLine 'cmd', '/c', 'git', 'rev-list', '--first-parent', '--count', 'master'
                    break
                case 'unix':
                    commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
                    break
            }
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    } catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            switch (ant.properties.os) {
                case 'windows':
                    commandLine 'cmd', '/c', 'git', 'describe', '--tags', '--dirty', '--always'
                    break
                case 'unix':
                    commandLine 'git', 'describe', '--tags', '--dirty', '--always'
                    break
            }
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null;
    }
}

android {
    compileSdkVersion(25)

    defaultConfig {
        applicationId("com.genesis.coletor.android")
        minSdkVersion(16)
        targetSdkVersion(22)
        versionCode LEGACY_BUILD_NUMBER_OFFSET + getMasterCommitCount()
        versionName getVersionName()
        testInstrumentationRunner("android.support.test.runner.AndroidJUnitRunner")
        multiDexEnabled true
    }

    buildTypes {
        release {
            // Proguard rules will need to be carefully revised before turning on minification --
            // the current configuration breaks Google Drive API.
            // minifyEnabled(true)
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            debuggable(true)
            // Allows AndroidTest JaCoCo reports to be generated
            testCoverageEnabled(true)
        }
    }

    packagingOptions {
        // Pick first occurrence of any files that cause conflicts, as defined
        // in common.gradle
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.ObjectCodec'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyReader'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyWriter'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        javaMaxHeapSize "2048M"
    }

    lintOptions {
        abortOnError true
    }
    buildToolsVersion '27.0.3'
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}



configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
    transitive = true
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
    }
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation 'com.google.android.gms:play-services-analytics:10.0.1'
    implementation 'com.google.android.gms:play-services-maps:10.0.1'
    implementation 'com.google.android.gms:play-services-auth:10.0.1'
    implementation 'com.android.support:multidex:1.0.1'
    implementation 'commons-io:commons-io:2.4'
    implementation 'net.sf.kxml:kxml2:2.3.0'
    implementation 'net.sf.opencsv:opencsv:2.3'
    implementation 'org.slf4j:slf4j-android:1.6.1-RC1'
    implementation 'com.google.code.gson:gson:2.6.2'
    implementation 'org.osmdroid:osmdroid-android:4.2'
    implementation 'joda-time:joda-time:2.9.7'
    implementation 'com.android.support:appcompat-v7:25.1.0'
    implementation 'pub.devrel:easypermissions:0.2.1'
    implementation('com.google.apis:google-api-services-sheets:v4-rev463-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation 'com.google.http-client:google-http-client:1.22.0'
    implementation 'com.google.oauth-client:google-oauth-client:1.22.0'
    implementation 'com.google.http-client:google-http-client-android:1.22.0'
    implementation 'com.google.api-client:google-api-client:1.22.0'
    // Testing-only dependencies
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'org.robolectric:robolectric:3.1.2'
    androidTestImplementation 'org.robolectric:shadows-multidex:3.0'
    androidTestImplementation 'com.android.support:support-annotations:25.1.0'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
}

apply plugin: 'com.google.gms.google-services'
